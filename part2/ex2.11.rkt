#lang racket

;; Exercise 2.11 http://sarabander.github.io/sicp/html/2_002e1.xhtml#Exercise-2_002e11

(require "ex2.7.rkt")

(define (mul-interval x y)
  (define (neg-neg-interval? x)
    (and (< (lower-bound x) 0)
         (< (upper-bound x) 0)))
  (define (nneg-nneg-interval? x)
    (and (>= (lower-bound x) 0)
         (>= (upper-bound x) 0)))
  (cond ((neg-neg-interval? x)
           (cond ((neg-neg-interval? y)
                    (make-interval (* (upper-bound x)
                                      (upper-bound y))
                                   (* (lower-bound x)
                                      (lower-bound y))))
                 ((nneg-nneg-interval? y)
                    (make-interval (* (lower-bound x)
                                      (upper-bound y))
                                   (* (upper-bound x)
                                      (lower-bound y))))
                 (else
                    (make-interval (* (lower-bound x)
                                      (upper-bound y))
                                   (* (lower-bound x)
                                      (lower-bound y))))))
        ((nneg-nneg-interval? x)
           (cond ((neg-neg-interval? y)
                    (make-interval (* (upper-bound x)
                                      (lower-bound y))
                                   (* (lower-bound x)
                                      (upper-bound y))))
                 ((nneg-nneg-interval? y)
                    (make-interval (* (lower-bound x)
                                      (lower-bound y))
                                   (* (upper-bound x)
                                      (upper-bound y))))
                 (else
                    (make-interval (* (upper-bound x)
                                      (lower-bound y))
                                   (* (upper-bound x)
                                      (upper-bound y))))))
        (else
           (cond ((neg-neg-interval? y)
                    (make-interval (* (upper-bound x)
                                      (lower-bound y))
                                   (* (lower-bound x)
                                      (lower-bound y))))
                 ((nneg-nneg-interval? y)
                    (make-interval (* (lower-bound x)
                                      (upper-bound y))
                                   (* (upper-bound x)
                                      (upper-bound y))))
                 (else
                    (let ((ll (* (lower-bound x)
                                 (lower-bound y)))
                          (lu (* (lower-bound x)
                                 (upper-bound y)))
                          (ul (* (upper-bound x)
                                 (lower-bound y)))
                          (uu (* (upper-bound x)
                                 (upper-bound y))))
                      (make-interval (min ul lu)
                                     (max ll uu))))))))

(module+ test
  (require rackunit)
  (let ((nn (make-interval -3 -2))
        (pp (make-interval 2 3))
        (np (make-interval -2 3))
        (np2 (make-interval -4 5))
        (nz (make-interval -2 0))
        (zp (make-interval 0 3)))
    (check-equal? (mul-interval nn nn) (make-interval 4 9))
    (check-equal? (mul-interval nn pp) (make-interval -9 -4))
    (check-equal? (mul-interval nn np) (make-interval -9 6))

    (check-equal? (mul-interval pp nn) (make-interval -9 -4))
    (check-equal? (mul-interval pp pp) (make-interval 4 9))
    (check-equal? (mul-interval pp np) (make-interval -6 9))

    (check-equal? (mul-interval np nn) (make-interval -9 6))
    (check-equal? (mul-interval np pp) (make-interval -6 9))
    (check-equal? (mul-interval np np2) (make-interval -12 15))

    (check-equal? (mul-interval nz nn) (make-interval 0 6))
    (check-equal? (mul-interval nz pp) (make-interval -6 0))
    (check-equal? (mul-interval nz np) (make-interval -6 4))

    (check-equal? (mul-interval zp nn) (make-interval -9 0))
    (check-equal? (mul-interval zp pp) (make-interval 0 9))
    (check-equal? (mul-interval zp np) (make-interval -6 9))))

